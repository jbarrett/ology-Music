#!/usr/bin/env perl
use strict;
use warnings;

use Math::Curve::Hilbert;
use lib '/Users/gene/sandbox/MIDI-Util/lib';
use MIDI::Util qw(setup_score);
use lib '/Users/gene/sandbox/Music-ScaleNote/lib';
use Music::ScaleNote;
use Music::Note;

my $max     = shift || 64;
my $bpm     = shift || 120;
my $t_scale = shift || 'dorian';
my $b_scale = shift || 'pminor';

my $score = setup_score( bpm => $bpm );

my $format = 'midinum';

my $treb = 60;
my $bass = 48;

my $t_note = Music::Note->new( $treb, $format );
my $b_note = Music::Note->new( $bass, $format );

my $t_msn = Music::ScaleNote->new(
    scale_note  => $t_note->format('isobase'),
    scale_name  => $t_scale,
    note_format => $format,
#    verbose     => 1,
);

my $b_msn = Music::ScaleNote->new(
    scale_note  => $b_note->format('isobase'),
    scale_name  => $b_scale,
    note_format => $format,
#    verbose     => 1,
);

my $hilbert = Math::Curve::Hilbert->new(
    direction => 'up',
    max       => 8,
    clockwise => 1,
    step      => 4,
);

my $count = 0;

# Start the Hilbert curve
my ( $x1, $y1 ) = $hilbert->CoordinatesFromPoint( $count++ );

# Draw the Hilbert curve
while ( ( $hilbert->CoordinatesFromPoint($count) )[0] ) {
    $score->n( 'qn', $treb, $bass );

    # Get a new point on the curve
    my ( $x2, $y2 ) = $hilbert->CoordinatesFromPoint( $count++ );

    # Inc/Decrement the treble note
    if ( $x2 - $x1 > 0 ) {
        $treb = $t_msn->get_offset(
            note_name => $treb,
            offset    => 1,
        )->format($format);
    }
    elsif ( $x2 - $x1 < 0 ) {
        $treb = $t_msn->get_offset(
            note_name => $treb,
            offset    => -1,
        )->format($format);
    }

    # Inc/Decrement the bass note
    if ( $y2 - $y1 > 0 ) {
        $bass = $b_msn->get_offset(
            note_name => $bass,
            offset    => 1,
        )->format($format);
    }
    elsif ( $y2 - $y1 < 0 ) {
        $bass = $b_msn->get_offset(
            note_name => $bass,
            offset    => -1,
        )->format($format);
    }

    # Increment the line segment
    ( $x1, $y1 ) = ( $x2, $y2 );

    # End the loop if we have reached the maximum
    last if $count > $max;
}

$score->write_score("$0.mid");
