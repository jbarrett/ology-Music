#!/usr/bin/env perl
use strict;
use warnings;

use MIDIUtil;
use Math::Curve::Hilbert;

my $max = shift || 64;
my $bpm = shift || 120;

my $score = MIDIUtil::setup_midi( bpm => $bpm );

my $hilbert = Math::Curve::Hilbert->new(
    direction => 'up',
    max       => 8,
    clockwise => 1,
    step      => 4,
);

my $note  = 60;
my $count = 0;

# Start the Hilbert curve
my ( $x1, $y1 ) = $hilbert->CoordinatesFromPoint( $count++ );

# Draw the Hilbert curve
while ( ( $hilbert->CoordinatesFromPoint($count) )[0] ) {
    $score->n( 'qn', $note );

    # Get a new point on the curve
    my ( $x2, $y2 ) = $hilbert->CoordinatesFromPoint( $count++ );

    # Inc/Decrement the note
    if ( $x2 - $x1 > 0 ) {
        $note += 2;
    }
    elsif ( $x2 - $x1 < 0 ) {
        $note -= 2;
    }

    # Increment the line segment
    ( $x1, $y1 ) = ( $x2, $y2 );

    # End the loop if we have reached the maximum
    last if $count > $max;
}

$score->write_score("$0.mid");
