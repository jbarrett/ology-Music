#!/usr/bin/env perl
use strict;
use warnings;

use lib $ENV{HOME} . '/sandbox/MIDI-Util/lib';
use MIDI::Util;
use Math::BaseConvert;
use Math::Prime::XS qw(primes);
use Music::Scales;

my $patch = shift || 42;
my $bpm   = shift || 300;
my $limit = shift || 500;  # Max number of primes
my @scale = @ARGV; # C4 Ds4 F4 G4 As4 C5 <- Example notes to choose from

if (!@scale) {
    @scale = get_scale_notes('C', 'major');
    @scale = map { $_ . 4 } @scale;
    for (@scale) {
        s/#/s/;
        s/b/f/;
    }
}

my $notes = join '|', @scale;  # "OR regular expression" of scale notes

Math::BaseConvert::dig(\@scale); # Set the base digits to the scale notes

my $score = MIDI::Util::setup_score(patch => $patch, bpm => $bpm);

# For each prime number...
for my $n (primes($limit)) {
    my $x = cnv($n, 10, scalar(@scale));  # Base-convert the decimal to a note string
    my @phrase = $x =~ /($notes)/g;  # Separate the note string into an array

    # Output the progress:
    printf "%d => %s (%d)\n", $n, join(' ', @phrase), scalar(@phrase);

    $score->n('qn', $_) for @phrase;
}

$score->write_score("$0.mid");
