#!/usr/bin/env perl
use strict;
use warnings;

# Lookup stuff on spotify

# settings.yml example:
# oauth_client_id: abcdefghijklmnopqrstuvwxyz
# oauth_client_secret: 12345678901234567890

use Data::Dumper::Compact qw(ddc);
use Getopt::Long qw(GetOptions);
use WWW::Spotify;
use YAML::XS qw(LoadFile);

my %opts = (
    artist => undef,
    album  => undef,
    track  => undef,
    config => 'settings.yml',
    limit  => 10,
    max    => 50,
);
GetOptions( \%opts,
    'config=s',
    'artist=s',
    'album=s',
    'track=s',
    'limit=i',
    'max=i',
);

die qq/Usage: perl $0 config="settings.yml" artist="Gene Boggs" album="X" track="Clubster"\n/
    unless $opts{artist} || $opts{album} || $opts{track};

my $cfg = LoadFile($opts{config});

my $spotify = WWW::Spotify->new;
$spotify->oauth_client_id($cfg->{oauth_client_id});
$spotify->oauth_client_secret($cfg->{oauth_client_secret});
$spotify->force_client_auth(1);
$spotify->auto_json_decode(1);

my $key   = $opts{track} ? 'track' : $opts{album} ? 'album' : 'artist';
my $keys  = $key . 's';
my @query = ($opts{$key}, $key);

my ($limit, $max) = @opts{ qw(limit max) };
 
BATCH: for (my $i = 0; $i <= $max; $i += $limit) {
    my $batch = $max - $i < $limit ? $max - $i + 1 : $limit;
    warn "offset=$i, limit=$batch\n";

    my $result = $spotify->search(
        @query,
        { limit => $batch, offset => $i }
    );

    for my $item ($result->{$keys}{items}->@*) {
        if ($key eq 'artist' && $item->{name} eq $opts{artist}) {
            print 'Genres: ', join(', ', $item->{genres}->@*), "\n",
                "Image: $item->{images}[1]{url}\n";
            last BATCH;
        }
        elsif ($item->{name} =~ /^$opts{$key}\b/
            && defined $item->{artists}[0]{name}
            && $item->{artists}[0]{name} eq $opts{artist}
        ) {
            my $val = $key eq 'track'
                ? $item->{album}{images}[1]{url}
                : $item->{images}[1]{url};
            print "Image: $val\n";
            last BATCH;
        }
    }
}
