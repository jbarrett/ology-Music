#!/usr/bin/env perl

# Requires Math::Fractal::Noisemaker

# Write up: http://techn.ology.net/hearing-an-image-one-pixel-at-a-time/

use strict;
use warnings;

use Imager;
use MIDI::Simple;
use lib '/Users/gene/sandbox/MIDI-Util/lib';
use MIDI::Util; # https://metacpan.org/release/MIDI-Util

my $type = shift || 'worley'; # Noisemaker type
my $size = shift || 8; # Side width of Noisemaker image
my $bpm  = shift || 100; # Beats per minute

# Get a MIDI score
my $score = MIDI::Util::setup_score(bpm => $bpm, patch => 5);

my $file = 'make-noise.png';

# Make a grayscale fractal image
system('make-noise', '-out', $file, '-type', $type, '-len', $size, '-format', 'png') == 0
    or die "system failed: $?";

# Open the image
my $img = Imager->new;
$img->read(file => $file)
    or die "Can't read $file: ", $img->errstr;

# Remove the image file
unlink $file;

# Add a note to the score for each pixel color
for my $y (0 .. $size - 1) {
    for my $x (0 .. $size - 1) {
        my $color = $img->getpixel(x => $x, y => $y);
        my ($red) = $color->rgba;
        my $n = sprintf '%.0f', scale($red);
        print "[$x,$y] $red -> $n\n";
        $score->n('qn', $n);
    }
}

$score->write_score("$0.mid");

# Scale a number from one range (grays) to another (midi)
sub scale {
    my $m = shift;
    my ($r_min, $r_max) = (0, 255); # Grayscale range
    my ($t_min, $t_max) = (60, 83); # MIDI target range starting at middle C
    return (($m - $r_min) / ($r_max - $r_min)) * ($t_max - $t_min) + $t_min;
}

__END__
Noise Types:

  * white           ## pseudo-random values
  * wavelet         ## band-limited ortho
  * gradient        ## persistent gradient noise
  * simplex         ## continuous gradient noise
  * simplex2        ## interpolated simplex
  * square          ## diamond-square algorithm
  * gel             ## self-displaced smooth
  * sgel            ## self-displaced diamond-square
  * dmandel         ## "deep" mandelbrot
  * djulia          ## "deep" julia
  * dla             ## diffusion-limited aggregation
  * worley          ## voronoi cell noise
  * wgel            ## self-displaced cell noise

  ! multires        ## multi-resolution
  ! ridged          ## ridged multifractal
  ! block           ## unsmoothed multi-res
  ! pgel            ## self-displaced multi-res
  ! fur             ## inspired by "Perlin Worms"
  ! tesla           ## worms/fur variant
