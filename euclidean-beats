#!/usr/bin/env perl
use strict;
use warnings;

# Play Euclidean word sets where the onsets are unique prime numbers
# less than or equal to the total number of beats.

use Data::Dumper::Compact qw(ddc);
use Math::Prime::Util::PrimeIterator ();
use lib map { "$ENV{HOME}/sandbox/$_/lib" } qw(MIDI-Drummer-Tiny Music-CreatingRhythms); # local author libs
use MIDI::Drummer::Tiny ();
use Music::CreatingRhythms ();

my $n     = shift || 16;  # number of terms
my $loops = shift || 8;   # times to loop
my $bpm   = shift || 90;  # beats per minute
my $vol   = shift || 100; # volume 0-127

my $mcr = Music::CreatingRhythms->new;

my $d = MIDI::Drummer::Tiny->new(
   file   => "$0.mid",
   bpm    => $bpm,
   volume => $vol,
   bars   => $loops,
   reverb => 15,
);

# get a list of primes less than or equal to N
my @primes;
my $it = Math::Prime::Util::PrimeIterator->new;
my $p = $it->iterate;
while ($p <= $n) {
    push @primes, $p;
    $p = $it->iterate;
}

# get the number of onsets for the kick drum
my $kick_p = 0;
while ($kick_p < 3) {
    $kick_p = $primes[ int rand @primes ];
}

# get the number of onsets for the snare drum
my $snare_p = $kick_p;
while ($snare_p >= $kick_p) {
    $snare_p = $primes[ int rand @primes ];
}

# play all the parts simultaneously
$d->sync(
    \&hihat,
    \&snare_drum,
    \&kick_drum,
);

# write the score to a midi file
$d->write;

sub hihat {
    my $x = int $n / 2;
    my $sequence = [ (1) x $x ];
    print "1/$x Hihat: ", ddc($sequence);
    for (1 .. $d->bars) {
        for my $i (@$sequence) {
            $i ? $d->note('en', $d->closed_hh) : $d->rest('en');
        }
    }
}

sub snare_drum {
    my $sequence = $mcr->euclid($snare_p, $n);
    print "1/$n Snare ($snare_p, $n): ", ddc($sequence);
    for (1 .. $d->bars) {
        for my $i (@$sequence) {
            $i ? $d->note('sn', $d->snare) : $d->rest('sn');
        }
    }
}

sub kick_drum {
    my $sequence = $mcr->euclid($kick_p, $n);
    print "1/$n Kick  ($kick_p, $n): ", ddc($sequence);
    for (1 .. $d->bars) {
        for my $i (@$sequence) {
            $i ? $d->note('sn', $d->kick) : $d->rest('sn');
        }
    }
}
