#!/usr/bin/env perl
use strict;
use warnings;

use Music::BachChoralHarmony;
use List::Util qw( any );
use Graph::Easy;

my $id = shift || '003907bv';

# From https://archive.ics.uci.edu/ml/datasets/Bach+Choral+Harmony
my $file = shift || '/Users/gene/Documents/data/jsbach_chorals_harmony/jsbach_chorals_harmony.data';

# Read-in the chord progression of each song
my $bach = Music::BachChoralHarmony->new;
my $progression = $bach->parse;

# Find the bigrams of the chord progression
my %seen;

for my $song ( keys %$progression ) {
    next unless $song eq $id;  # Comment this line to get an enormous network of all song transitions

    my $last = '';

    for my $cluster ( @{ $progression->{$song}{events} } ) {
        my $notes = $cluster->{notes};
        my $bass  = $cluster->{bass};
        my $chord = $cluster->{chord};

        if ( $last && !any { $last eq $_ } @{ $seen{$chord} } ) {
            push @{ $seen{$last} }, $chord;
        }

        $last = $chord;
    }
}
#use Data::Dumper;warn(__PACKAGE__,' ',__LINE__," MARK: ",Dumper\%seen); exit;

# Make a network graph of the chord progression
my $graph = Graph::Easy->new();

for my $i ( keys %seen ) {
    my $from = Graph::Easy::Node->new( name => $i );

    for my $j ( @{ $seen{$i} } ) {
        my $to = Graph::Easy::Node->new( name => $j );
        $graph->add_edge( $from, $to );
    }
}

print $graph->as_graphviz();

__END__
perl % > bach-choral-network.dot
dot -Tpng bach-choral-network.dot > bach-choral-network.png
open bach-choral-network.png
