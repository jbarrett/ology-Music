#!/usr/bin/env perl
use strict;
use warnings;

use MIDIUtil; # Found in the same github directory...
use lib '/Users/gene/sandbox/Music-ScaleNote/lib';
use Music::ScaleNote;
use Music::Note;
use Music::Scales;

my $rule       = shift || 2,
my $iterations = shift || 4;
my $duration   = shift || 'qn';
my $offset     = shift || 1;
my $scale      = shift || 'major';
my $midi_note  = shift || 60;
my $bpm        = shift || 300;

$duration = [ split /\s+/, $duration ];

my $format = 'midinum';
my $patchf = 0;
my $patchg = 13;

my %rules = (
    # Branches
    1 => {
        X => 'YF-X+X',
        Y => 'F',
    },

    # Koch curve
    2 => {
        F => 'F+F-F-F+F',
    },

    # Fractal plant
    3 => {
        X => 'F-XXF-X+FX',
        F => 'FF',
    },

    # Dragon curve
    4 => {
        X => 'X+YF+',
        Y => '-FX-Y',
    },

    # Sierpiński arrowhead curve
    5 => {
        F => 'G-F-G',
        G => 'F+G+F',
    },

    # Sierpiński triangle
    6 => {
        F => 'F-G+F+G-F',
        G => 'GG',
    },

    # Koch snowflake
    7 => {
        F => 'F-F++F-F',
        X => 'FF',
    },

    # Sierpiński carpet
    8 => {
        F => 'F+F-F-F-G+F+F+F-F',
        G => 'GGG',
    },

    # Koch island
    9 => {
        F => 'F-F+F+FF-F-F+F',
    },

    # Koch islands and lakes
    10 => {
        F => 'F+f-FF+F+FF+Ff+FF-f+FF-F-FF-Ff-FFF',
        f => 'ffffff',
    },

    # Grid
    11 => {
        F => 'FF-F-F-F-FF',
    },

    # Terndrils
    12 => {
        F => 'FF-F--F-F',
    },
);

my %axioms = (
    1  => 'X',
    2  => 'F',
    3  => 'X',
    4  => 'FX',
    5  => 'F',
    6  => 'F-G-G',
    7  => 'F++F++F',
    8  => 'F',
    9  => 'F-F-F-F',
    10 => 'F+F+F+F',
    11 => 'F-F-F-F',
    12 => 'F-F-F-F',
);

my $string = $axioms{$rule};

my $score = MIDIUtil::setup_midi( bpm => $bpm );

my $note = Music::Note->new( $midi_note, $format );

my $msn = Music::ScaleNote->new(
    scale_note => $note->format('isobase'),
    scale_name => $scale,
);

my %translate = (
    'f' => sub { $score->r( random_duration( @$duration ) ) },
    'F' => sub {
        MIDIUtil::set_chan_patch( $score, 0, $patchf );
        $score->n( random_duration( @$duration ), $midi_note );
    },
    'G' => sub {
        MIDIUtil::set_chan_patch( $score, 0, $patchg );
        $score->n( random_duration( @$duration ), $midi_note );
    },
    '-' => sub {
        $midi_note = $msn->get_offset(
            note_name   => $midi_note,
            note_format => $format,
            offset      => $offset,
        )->format($format);
    },
    '+' => sub {
        $midi_note = $msn->get_offset(
            note_name   => $midi_note,
            note_format => $format,
            offset      => -$offset,
        )->format($format);
    },
);

for ( 1 .. $iterations ) {
    $string =~ s/(.)/defined($rules{$rule}{$1}) ? $rules{$rule}{$1} : $1/eg;
}
warn "$string\n";

for my $command ( split //, $string ) {
    $translate{$command}->() if exists $translate{$command};
}

$score->write_score( $0 . '.mid' );

sub random_duration {
    my (@duration) = @_;
    return $duration[ int rand @duration ];
}
