#!/usr/bin/env perl
use strict;
use warnings;

use MIDIUtil; # Found in the same github directory...
#use lib '/Users/gene/sandbox/Music-ScaleNote/lib';
use Music::ScaleNote;
use Music::Note;

my $rule       = shift || 2,        # The rule number in the list of rules below
my $iterations = shift || 4;        # The number of iterations of the fractal curve
my $duration   = shift || 'qn';     # The space separated list of durations from which to choose
my $midi_note  = shift || 60;       # The Initial midinum format note. 60 = Middle C
my $offset     = shift || 1;        # Distance to move in the scale for a new note value
my $scale      = shift || 'major';  # The name of the scale to traverse
my $bpm        = shift || 300;      # The beats per minute of the rendered MIDI

# Split the durations into a list so that they can be randomly selected
$duration = [ split /\s+/, $duration ];

# Default variables
my $format = 'midinum';
my $patchf = 0;
my $patchg = 13;

# The master list of fractals by rule number, their axioms and production rules
my %rules = (
    1 => { # Branches
        axiom => 'X',
        X => 'YF-X+X',
        Y => 'F',
    },
    2 => { # Koch curve
        axiom => 'F',
        F => 'F+F-F-F+F',
    },
    3 => { # Fractal plant
        axiom => 'X',
        X => 'F-XXF-X+FX',
        F => 'FF',
    },
    4 => { # Dragon curve
        axiom => 'FX',
        X => 'X+YF+',
        Y => '-FX-Y',
    },
    5 => { # Sierpiński arrowhead curve
        axiom => 'F',
        F => 'G-F-G',
        G => 'F+G+F',
    },
    6 => { # Sierpiński triangle
        axiom => 'F-G-G',
        F => 'F-G+F+G-F',
        G => 'GG',
    },
    7 => { # Koch snowflake
        axiom => 'F++F++F',
        F => 'F-F++F-F',
        X => 'FF',
    },
    8 => { # Sierpiński carpet
        axiom => 'F',
        F => 'F+F-F-F-G+F+F+F-F',
        G => 'GGG',
    },
    9 => { # Koch island
        axiom => 'F-F-F-F',
        F => 'F-F+F+FF-F-F+F',
    },
    10 => { # Koch islands and lakes
        axiom => 'F+F+F+F',
        F => 'F+f-FF+F+FF+Ff+FF-f+FF-F-FF-Ff-FFF',
        f => 'ffffff',
    },
    11 => { # Grid
        axiom => 'F-F-F-F',
        F => 'FF-F-F-F-FF',
    },
    12 => { # Terndrils
        axiom => 'F-F-F-F',
        F => 'FF-F--F-F',
    },
);

# Get the axiom to use based on the given rule
my $string = $rules{$rule}{axiom};

# Prepare to create MIDI
my $score = MIDIUtil::setup_midi( bpm => $bpm );

# Create a note object for the given start note value
my $note = Music::Note->new( $midi_note, $format );

# Create a scale-note object to use to traverse the given scale
my $msn = Music::ScaleNote->new(
    scale_note => $note->format('isobase'),
    scale_name => $scale,
#    verbose    => 1,
);

# The dispatch table of MIDI routines based on "turtle graphic" moves
my %translate = (
    # Add a rest to the score
    'f' => sub { $score->r( random_duration( @$duration ) ) },
    # Add a note to the score
    'F' => sub {
        MIDIUtil::set_chan_patch( $score, 0, $patchf );
        $score->n( random_duration( @$duration ), $midi_note );
    },
    # Add a note to the score
    'G' => sub {
        MIDIUtil::set_chan_patch( $score, 1, $patchg );
        $score->n( random_duration( @$duration ), $midi_note );
    },
    # Decrement the scale-note
    '-' => sub {
        $midi_note = $msn->get_offset(
            note_name   => $midi_note,
            note_format => $format,
            offset      => -$offset,
        )->format($format);
    },
    # Increment the scale-note
    '+' => sub {
        $midi_note = $msn->get_offset(
            note_name   => $midi_note,
            note_format => $format,
            offset      => $offset,
        )->format($format);
    },
);

# Apply the string re-writing production rules
for ( 1 .. $iterations ) {
    $string =~ s/(.)/defined($rules{$rule}{$1}) ? $rules{$rule}{$1} : $1/eg;
}
warn "$string\n";

# Execute the routines defined by the string elements
for my $command ( split //, $string ) {
    last if $midi_note < 0 || $midi_note > 127;
    $translate{$command}->() if exists $translate{$command};
}

# Write the MIDI file
$score->write_score( $0 . '.mid' );

# Return a random duration.  * This makes things disjointed and strange. YMMV
sub random_duration {
    my (@duration) = @_;
    return $duration[ int rand @duration ];
}
