#!/usr/bin/env perl
use strict;
use warnings;

use MIDI::Simple;
use Math::Combinatorics;
use List::Util qw( shuffle );
use List::UtilsBy qw( zip_by );

my @treb_notes = qw( C5 D5 E5 );
my @bass_notes = qw( C3 F3 G3 );
my @durations  = qw( en qn dqn );

my @treb_combos = shuffle permute(@treb_notes);
my @bass_combos = shuffle permute(@bass_notes);
my @dura_combos = shuffle permute(@durations);

my $score = setup_midi( scalar(@treb_notes), 1, 0, 120 );

for my $i ( 0 .. @treb_combos - 1 ) {
    my @tuples = zip_by { [ $_[0], $_[1], $_[2] ] }
        $dura_combos[$i], $treb_combos[$i], $bass_combos[$i];
    for my $tuple ( @tuples ) {
        $score->n(@$tuple);
    }
}

$score->write_score($0.'.mid');

sub setup_midi {
    my ( $sig, $instrument, $patch, $volume ) = @_;
    my $score = MIDI::Simple->new_score();
    $score->Volume($volume);
    # Lead-in
    $score->Channel(9);
    $score->n( 'qn', 38 ) for 1 .. $sig;
    # Passage
    $score->Channel($instrument);
    $score->patch_change( $instrument, $patch );
    return $score;
}
