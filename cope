#!/usr/bin/env perl
use strict;
use warnings;

use Data::Dumper::Compact 'ddc';
use Algorithm::Combinatorics qw(combinations);
use Music::Chord::Namer qw(chordname);
use Music::Note;
use Music::Tension::Cope;

my $size = shift || 3;

my @notes = qw(C C# D D# E F F# G G# A A# B);

my $tension = Music::Tension::Cope->new;

my %tensions;

my @data = 0 .. 11;
my $iter = combinations(\@data, $size);

while (my $v = $iter->next) {
    my $c = chordname(map { $notes[$_] } @$v);
    my $t = $tension->vertical($v);
    
    $tensions{ join(' ', @$v) } = { chord => $c, tension => $t };
}

my $i = 0;
for my $chord (sort { $tensions{$a}->{tension} <=> $tensions{$b}->{tension} || $a cmp $b } keys %tensions) {
    $i++;
    print "$i. [$chord] => $tensions{$chord}->{chord} => $tensions{$chord}->{tension}\n";
}
