#!/usr/bin/env perl
use strict;
use warnings;

###
# Attempt at recreating the tape-loop effect of Brain Eno's "Music for Airports" (1978).
###

use lib map { "$ENV{HOME}/sandbox/$_/lib" } qw(MIDI-Util);
use MIDI::Util qw(setup_score set_chan_patch);

my $max = shift || 8; # How many phrase loops to render

my @notes = qw/F3 Gs3 C4 Cs4 Ds4 F4 Gs4/; # s=sharp. Key of Ab

my $ticks = 96 * 4; # = a whole note

my $factor = 4; # Number of whole note durations in a loop

my $channel = 0;

my $score = setup_score(lead_in => 0, bpm => 60);

my @phrases;

for my $note (@notes) {
    push @phrases, sub {
        set_chan_patch($score, $channel++, 73);
        phrase($score, $note, $ticks, $factor, $max);
    };
}

$score->synch(@phrases);

$score->write_score("$0.mid");

sub phrase {
    my ($score, $note, $ticks, $factor, $max) = @_;

    my $extra = int rand($ticks * 2);
    my $width = $ticks * $factor + $extra;
    my $head = int rand($width - $ticks);
    my $tail = $width - ($head + $ticks);
    printf "Note: %3s, Head=%d, Ticks=%d, Tail=%d, Total=%d\n",
        $note, $head, $ticks, $tail, $width;

    for my $i (1 .. $max) {
        $score->r('d' . $head) if $head;
        $score->n('d' . $ticks, $note);
        $score->r('d' . $tail) if $tail;
    }
}
