#!/usr/bin/env perl
use strict;
use warnings;

use lib '/Users/gene/sandbox/MIDI-Util/lib';
use MIDI::Util; # https://metacpan.org/release/MIDI-Util

use Game::Life;

my $bpm = shift || 100;

my $score = MIDI::Util::setup_score( bpm => $bpm );

my @source = qw/ C D E F G A B /;

my $size = @source;

# Create a LoL of source notes with incrementing octaves
my $octave = $size;
my $matrix;
while ( $octave ) {
    push @$matrix, [ map { $_ . $octave } @source ];
    $octave--;
}

my $glider = [
    [ 1, 1, 1 ],
    [ 1, 0, 0 ],
    [ 0, 1, 0 ]
];

my $g = Game::Life->new($size);
 
# Place the glider at the bottom-right of the grid
$g->place_points( 4, 4, $glider );

# Incrementally move the glider to the top-left of the grid
for ( 1 .. 17 ) {
    my $grid = $g->get_grid;

    # Find the note cluster to add to the score
    my @notes;
    my $i = 0;
    for my $row ( @$grid ) {
        my $j = 0;
        for my $element ( @$row ) {
            push @notes, $matrix->[$i][$j] if $element;
            $j++;
        }
        $i++;

        print map { $_ ? 'x' : '.' } @$row;
        print "\n";
    }
    print "@notes\n";
    print "\n";

    # Add the note cluster and a rest to the score
    $score->n( 'qn', @notes );
    $score->r('qn');

    $g->process;
}

$score->write_score( $0 . '.mid' );
