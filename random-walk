#!/usr/bin/env perl
use strict;
use warnings;

use Graph::Weighted;
use List::Util::WeightedChoice qw( choose_weighted );
use MIDI::Simple;

my $n = shift || 4;
my $initial = shift // 0;

my $pitch = Graph::Weighted->new();
$pitch->populate(
    {
        0 => { pitch => 'C4',  2 => 0.4, 6 => 0.6 },
        1 => { pitch => 'D4',  3 => 0.4, 4 => 0.6 },
        2 => { pitch => 'Ds4', 1 => 0.5, 3 => 0.5 },
        3 => { pitch => 'F4',  5 => 0.4, 4 => 0.6 },
        4 => { pitch => 'G4',  2 => 0.4, 3 => 0.6 },
        5 => { pitch => 'Gs4', 4 => 0.4, 6 => 0.6 },
        6 => { pitch => 'As4', 0 => 0.4, 3 => 0.6 },
    },
    'note'
);

my $duration = Graph::Weighted->new();
$duration->populate(
    {
        0 => { duration => 'qn', 2 => 0.4, 6 => 0.6 },
        1 => { duration => 'en', 3 => 0.4, 4 => 0.6 },
        2 => { duration => 'qn', 1 => 0.5, 3 => 0.5 },
        3 => { duration => 'qn', 5 => 0.4, 4 => 0.6 },
        4 => { duration => 'qn', 2 => 0.4, 3 => 0.6 },
        5 => { duration => 'en', 4 => 0.4, 6 => 0.6 },
        6 => { duration => 'qn', 0 => 0.4, 3 => 0.6 },
    },
    'time'
);

my $velocity = Graph::Weighted->new();
$velocity->populate(
    {
        0 => { velocity => 'mezzo', 2 => 0.4, 6 => 0.6 },
        1 => { velocity => 'mf',    3 => 0.4, 4 => 0.6 },
        2 => { velocity => 'mezzo', 1 => 0.5, 3 => 0.5 },
        3 => { velocity => 'mezzo', 5 => 0.4, 4 => 0.6 },
        4 => { velocity => 'mezzo', 2 => 0.4, 3 => 0.6 },
        5 => { velocity => 'mf',    4 => 0.4, 6 => 0.6 },
        6 => { velocity => 'mezzo', 0 => 0.4, 3 => 0.6 },
    },
    'amplitude'
);

my $score = setup_midi();

my $p_vertex = $initial;
my $d_vertex = $initial;
my $v_vertex = $initial;

for my $i ( 1 .. $n ) {
    my $note = $pitch->get_vertex_attribute( $p_vertex, 'pitch' );
    my $dura = $duration->get_vertex_attribute( $d_vertex, 'duration' );
    my $velo = $velocity->get_vertex_attribute( $v_vertex, 'velocity' );

    $score->n( $velo, $dura, $note );

    if ( $i < $n ) {
        $p_vertex = next_vertex( $pitch, $p_vertex, 'note' );
        $d_vertex = next_vertex( $duration, $d_vertex, 'time' );
        $v_vertex = next_vertex( $velocity, $v_vertex, 'amplitude' );
    }
}

$score->write_score( $0 . '.mid' );

sub setup_midi {
    my %args = (
        lead_in => 4,
        channel => 1,
        patch   => 42,
        volume  => 120,
        @_,
    );
    my $score = MIDI::Simple->new_score();
    $score->Volume($args{volume});
    # Lead-in
    $score->Channel(9);
    $score->n( 'qn', $args{patch} ) for 1 .. $args{lead_in};
    # Passage
    $score->Channel($args{channel});
    $score->patch_change( $args{channel}, $args{patch} );
    return $score;
}

sub next_vertex {
    my ( $g, $vertex, $attr ) = @_;

    my $successors = [];

    for my $successor ( $g->successors( $vertex, $attr ) ) {
        push @$successors, {
            vertex => $successor,
            weight => $g->get_cost( [ $vertex, $successor ], $attr ),
        };
   }

    my $choice = choose_weighted( $successors, sub{ $_[0]->{weight} } );

    return $choice->{vertex};
}
