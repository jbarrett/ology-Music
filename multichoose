#!/usr/bin/perl
#
# List all possible fret fingerings - gene@ology.net
#
use strict;
use warnings;

use List::MoreUtils qw( pairwise );
use Math::Combinatorics;

# See warnings in output context.
$|++;

# Set the maximum number of frets.
my $frets = shift || 4;

# Set the fret positions. 0 = open string & 4 is "frets from the start position.
my $positions = @ARGV ? \@ARGV : [qw( 0 1 2 3 4 )];

# Set the finger symbols. i=index m=middle r=ring p=pinky
#   Options: _=none x=muted *=harmonic
my $fingers = [qw( i m r p )];

# Make an object to hold all possible fret positions.
my $c = Math::Combinatorics->new(
    count => $frets,
    data => $positions,
    frequency => [ map{ $frets } 1 .. @$positions ],
);

# Line number format width.
my $n = 3; # TODO Compute with Math::Big?
# Loop counter for each fret group used in the final output.
my $i = 0;

# Visit each multiset.
while (my @m = $c->next_multiset) {
    # Make an object to hold this positions fret groups.
    my $p = Math::Combinatorics->new(
        data => \@m,
        frequency => [ map { 1 } @m ],
    );

    # Visit each set.
    while (my @s = $p->next_string) {
        # This is a string to hold reasons why this group is unplayable.
        my $flag = '';

        # Consider adjacent pairs of the fret group.
        for my $x (1 .. @s - 1) {
            # Hide the ugliness of array subscript indexing.
            my $previous = $s[$x - 1];
            my $current  = $s[$x];

            # "Next to the adjacent" can only span 2 frets. Etc.
            # TODO This logic restricts by fret, not finger, yet.
            # E.g. The middle-ring finger pair only spans 1 fret in most people.
            if ($previous && $current && $previous != $current
                && $current >= $previous + 2) {
                $flag = 's'; # Flag a finger span issue.
                last;
            }
        }

        # If this group has no "playability infractions", proceed.
        unless ($flag) {
            # The open position (0) is ignored. Remove zeros.
            my @no_z = grep { defined $_ && $_ } @s;

            if (@no_z >= 2) {
                # Consider pairs of the no-zero fret group.
                for my $x (1 .. @no_z - 1) {
                    # Consider the items to the tail.
                    my @sliver = @no_z[$x .. @no_z - 1];

                    # Higher fret numbers for lower fingers are not allowed.
                    if (grep { $no_z[$x - 1] > $_ } @sliver) {
                        $flag = 'b'; # Flag a backward issue
                        last;
                    }
                }
            }
        }

        # Concatinate fingers with frets.
        my @limited = @{$fingers}[0 .. @s - 1];
        my @pairs = pairwise { $a . $b } @limited, @s;

        # Print a string representation of the fret group.
        printf "%*d. %s %s\n", $n, ++$i, join(' ', @pairs), $flag;
    }
}

