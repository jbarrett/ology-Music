#!/usr/bin/perl
#
# List all possible fret fingerings - gene@ology.net
#
use strict;
use warnings;
use List::MoreUtils qw( pairwise zip );
use Math::Combinatorics;

# See warnings in output context.
$|++;

# Set the maximum number of frets.
my $frets = shift || 4;

# Set the numbers of the fret position, where 0 = open string
my $positions = @ARGV ? \@ARGV : [qw( 0 1 2 3 4 )];

# Set the finger symbols.
# Legend: i=index m=middle r=ring p=pinky _=none x=muted *=harmonic
my $fingers = [qw( i m r p )];

# Set the super set of multisets!
my $c = Math::Combinatorics->new(
    count => $frets,
    data => $positions,
    frequency => [ map{ $frets } 1 .. @$positions ],
);

# Line number format width.
my $n = 3; # TODO Compute length of perms with Math::Big?
# Loop counter for fret group.
my $i = 0;

# Visit each multiset.
while (my @m = $c->next_multiset) {
    # Consider each subset.
    my $p = Math::Combinatorics->new(
        data => \@m,
        frequency => [ map{1} @m ],
    );

    # Visit each subset.
    while (my @s = $p->next_string) {
        # Q: Is this a playable group?
        my $flag = ''; # A: Yes. So far...

        # Consider pairs of the fret group.
        for my $x (1 .. @s - 1) {
            # Less subscrpt referencing.
            my $previous = $s[$x - 1];
            my $current  = $s[$x];

            # The middle-ring finger pair can only span 1 fret.
            # "Next to the adjacent" can only span 2 frets. Etc.
            # TODO This logic restricts by fret, not finger, yet.
            if ($previous && $current
                && $previous != $current
                && $current >= $previous + 2
            ) {
                $flag = 's'; # Flag a span issue
                last;
            }
        }

        unless ($flag) {
            # The open position (0) is ignored. Remove zeros.
            my @no_z = grep { defined $_ && $_ } @s;

            if (@no_z >= 2) {
                # Consider pairs of the no-zero fret group.
                for my $x (1 .. @no_z - 1) {
                    # Consider the items to the tail.
                    my @sliver = @no_z[$x .. @no_z - 1];

                    # Higher fret numbers for lower fingers are not allowed.
                    if (grep { $no_z[$x - 1] > $_ } @sliver) {
                        $flag = 'b'; # Flag a backward issue
                        last;
                    }
                }
            }
        }

        # Concatinate fingers with frets.
        my @limited = @{$fingers}[0 .. @s - 1];
        my @pairs = pairwise { $a . $b } @limited, @s;

        # Print the string representation.
        printf "%*d. %s %s\n", $n, ++$i, join(' ', @pairs), $flag;
    }
}
# Consider the lilies of the field.
print "u Indicates an unplayable fingering\n";

