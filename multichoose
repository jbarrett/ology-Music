#!/usr/bin/perl
#
# List all possible fret fingerings - gene@ology.net
#
use strict;
use warnings;
use Math::Combinatorics;

# Set the size of the fret grid or "the maximum number of frets."
my $frets = shift || 4;
# Set the symbols to use for the fret position.
my $positions = @ARGV ? \@ARGV : [qw( 0 1 2 3 4 )];

# Set the super set of multisets!
my $c = Math::Combinatorics->new(
    count => $frets,
    data => $positions,
    frequency => [ map{ $frets } 1 .. @$positions ],
);

# Line number format width.
my $n = 3; # TODO Compute width with Math::Counting::bcomb()
# Loop counter for fret group.
my $i = 0;

# Visit each multiset.
while (my @m = $c->next_multiset) {
    # Consider each subset.
    my $p = Math::Combinatorics->new(
        data => \@m,
        frequency => [ map{1} @m ],
    );
    # Visit each subset.
    while (my @s = $p->next_string) {
        # Is this a playable group?
        my $flag = 0;
        for my $x (1 .. @s - 1) {
            if ($s[$x - 1] > $s[$x] && $s[$x] != 0) {
                $flag++;
                last;
            }
        }
        # Print the string representation.
        printf "%*d. %s%s\n", $n, ++$i, join(' ', @s), ($flag ? ' *' : '');
    }
}
print "* Indicates an unplayable fingering\n";

