#!/usr/bin/env perl
#
# "Musimathics" pg. 358 - Voss's Method
#
use strict;
use warnings;

use lib '/Users/gene/sandbox/MIDI-Util/lib';
use MIDI::Util;
use MIDI::Simple;

my $max = shift || 16;

my @tnotes = qw( C5 Ds5 F5 G5 As5 C6 );
my @bnotes = qw( C3 F3 G3 C4 );

my $score = MIDI::Util::setup_score();

my ( @treble, @bass );

my @tseed = map { int rand @tnotes } 1 .. 4;
my @bseed = map { int rand @bnotes } 1 .. 4;

$score->synch( \&treble, \&bass );

use Data::Dumper;print Dumper\@treble;
use Data::Dumper;print Dumper\@bass;

$score->write_score("$0.mid");

sub treble {
    MIDI::Util::set_chan_patch( $score, 0, 0 );
    for my $n ( 0 .. $max ) {
        $treble[$n] = $tnotes[ voss( scalar(@tnotes), $n, \@tseed ) ];
        $score->n( 'qn', $treble[$n] );
    }
}

sub bass {
    MIDI::Util::set_chan_patch( $score, 1, 32 );
    for my $n ( 0 .. $max ) {
        $bass[$n] = $bnotes[ voss( scalar(@bnotes), $n, \@bseed ) ];
        $score->n( 'qn', $bass[$n] );
    }
}

sub voss {
    my ( $top, $i, $list ) = @_;

    my $sum = 0;

    for my $k ( 0 .. @$list - 1 ) {
        if ( ( $i % ( 2 ** $k ) ) == 0 ) {
            $list->[$k] = int( rand $top );
        }
        $sum += $list->[$k];
    }

    return $sum % $top;
}
