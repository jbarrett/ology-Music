#!/usr/bin/env perl
use strict;
use warnings;

use Data::Dumper::Compact qw(ddc);
use lib map { "$ENV{HOME}/sandbox/$_/lib" } qw(MIDI-Drummer-Tiny MIDI-Praxis-Variation MIDI-Util Music-Chord-Progression Music-Duration-Partition);
use MIDI::Drummer::Tiny;
use MIDI::Praxis::Variation qw(transposition);
use MIDI::Util;
use Music::Chord::Progression;
use Music::Duration::Partition;
use Music::Scales qw(get_scale_notes get_scale_MIDI);

my $bars   = shift || 16;
my $bpm    = shift || 105;
my $note   = shift || 'C';
my $bscale = shift || 'pentatonic';
my $tscale = shift || 'major';
my $bpatch = shift || 35;
my $tpatch = shift || 69;

my $d = MIDI::Drummer::Tiny->new(
    file => "$0.mid",
    bpm  => $bpm,
    bars => $bars,
);

my @bass; # List of initial bottom notes
my @notes; # List of chord named notes with octave

$d->score->synch(
#    \&drums,
    \&progression,
    \&bottom,
    \&top,
);

$d->write;

sub drums {
    $d->count_in($bars * 2);
#    $d->metronome44($bars * 2);
    $d->note($d->whole, $d->crash1, $d->kick);
}

sub progression {
    MIDI::Util::set_chan_patch($d->score, 2, 0);

    my @pitches = get_scale_notes($note, $bscale);
    @notes = map { $pitches[int rand @pitches] } 1 .. 4;

    my $chords;
    if ($tscale eq 'major') {
        $chords = ['','m','m','','','m'];
    }
    else {
        $chords = ['m','','m','m','',''];
    }

    for my $x (@notes) {
        my $prog = Music::Chord::Progression->new(
            scale_note => $x,
            scale_name => $tscale,
            chords     => $chords,
#            substitute => 1,
#            verbose    => 1,
        );
        my $notes = $prog->generate;

        for my $chord (@$notes) {
            $chord = [ MIDI::Util::midi_format(@$chord) ];
            $d->score->n('wn', @$chord);
            push @bass, $chord->[0];
        }
        print $x, ': ', ddc($notes);
    }
}

sub bottom {
    MIDI::Util::set_chan_patch($d->score, 0, $bpatch);

    my @transp = transposition(-12, @bass);

    my $mdp = Music::Duration::Partition->new(
        size => 4,
        pool => [qw(dhn hn qn)],
    );

    my @phrases = map { $mdp->motif } 1 .. 4;

    for my $x (@transp) {
        my $phrase = $phrases[int rand @phrases];
        for my $n (0 .. $#$phrase) {
            if ($n % 2 == 0) {
                $d->score->n($phrase->[$n], $x);
            }
            else {
                $d->score->n($phrase->[$n], $x + 7);
            }
        }
    }
}

sub top {
    MIDI::Util::set_chan_patch($d->score, 1, $tpatch);

    my $mdp = Music::Duration::Partition->new(
        size => 4,
#        pool => [qw(dhn hn qn)], # "plaintive"
        pool => [qw(hn qn dqn en)],
    );

    my @phrases = map { $mdp->motif } 1 .. 4;

    for my $x (@notes) {
        my @pitches = get_scale_notes($x, $tscale);
        @pitches = MIDI::Util::midi_format(@pitches);
        for my $i (1 .. 8) { # NOTE 8 is the default Music::Chord::Progression max
            my $phrase = $phrases[int rand @phrases];
            for my $n (0 .. $#$phrase) {
                $d->score->n($phrase->[$n], $pitches[int rand @pitches] . 5);
            }
        }
    }
}
