#!/usr/bin/env perl

# Show the top repeated note phrases of a MIDI file.

use strict;
use warnings;

use MIDI;
use Music::Note;
use Lingua::EN::Ngram;

my $file = shift || die "Usage: perl $0 /some/file.mid size max";
my $size = shift || 2;  # ngram size
my $max  = shift || 40; # -1 for all >1 records

my $opus = MIDI::Opus->new( { from_file => $file } );
#$opus->dump( { dump_tracks => 1 } );

# Counter for the tracks seen
my $i = 0;

# Handle each track...
for my $t ( $opus->tracks ) {
    # Collect the note events for each track
    my @events = grep { $_->[0] eq 'note_on' } $t->events;

    # Skip if there are no events
    next unless @events;

    $i++;
    print "$t $i\n";

    # Declare the notes to inspect
    my $text = '';

    # Accumulate the notes
    for my $event ( @events ) {
        my $note = Music::Note->new( $event->[3], 'midinum' );
        $text .= $note->format('midi') . ' ';
    }

    # Parse the note text into ngrams
    my $ngram  = Lingua::EN::Ngram->new( text => $text );
    my $phrase = $ngram->ngram($size);

    # Counter for the ngrams seen
    my $j = 0;

    # Display the ngrams in order of their repetition amount
    for my $p ( sort { $phrase->{$b} <=> $phrase->{$a} } keys %$phrase ) {
        last if $phrase->{$p} == 1;

        $j++;

        # End if we are past the maximum
        last if $max > 0 && $j > $max;

        printf "\t%d.\t%d\t%s\n", $j, $phrase->{$p}, $p;
    }
}
