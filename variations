#!/usr/bin/env perl
use strict;
use warnings;

use Algorithm::Combinatorics qw( variations );
use List::Util qw( shuffle );
use MIDIUtil;
 
my $treb  = shift || '0 C5 E5 G5';
my $bass  = shift || 'C4 F4 G4';
my $bpm   = shift || 120;
my $trebp = shift || 69;
my $bassp = shift || 42;

my @treb = split / /, $treb;
my @bass = split / /, $bass;

my $size = 0;

my $score = MIDIUtil::setup_midi( bpm => $bpm );

$score->synch(
    \&variance,
    \&legato,
);

$score->n( 'wn', $bass[0] );
$score->write_score( "$0.mid" );

sub variance {
    MIDIUtil::set_chan_patch( $score, 0, $trebp );

    my $count = 1;

    for my $n ( 1 .. @treb ) {
        my @items = shuffle variations( \@treb, $n );

        for my $i (@items) {
            print "$count. @$i\n";
            $count++;

            for my $note ( @$i ) {
                if ( $note eq '0' ) {
                    $score->r('qn');
                }
                else {
                    $score->n( 'qn', $note );
                }

                $size++;
            }
        }
    }
}

sub legato {
    MIDIUtil::set_chan_patch( $score, 1, $bassp );
    $score->Volume(60);

    my @notes = grep { $_ ne '0' } @bass;

    for my $n ( 1 .. $size / 4 ) {
        $score->n( 'wn', $notes[ int rand @notes ] );
    }
}
